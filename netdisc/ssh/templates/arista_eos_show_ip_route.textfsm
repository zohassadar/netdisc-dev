Value Filldown VRF (\S+)
Value PROTOCOL (\S+\s\S+?|\w?)
Value Required NETWORK (\d+\.\d+\.\d+\.\d+)
Value MASK (\d+)
Value DISTANCE (\d+)
Value METRIC (\d+)
Value DIRECT (directly)
Value Required,List NEXT_HOP (connected|\d+\.\d+\.\d+\.\d+)
#Value INTERFACE (\S+)
Value List INTERFACE (.+)
Value VNI (\d+)
Value ROUTER_MAC ((?:[0-9a-f]{2}:){5}[0-9a-f]{2})

Start
  ^VRF(\s+name)?:\s+${VRF}\s*$$
  ^WARNING
  ^kernel
  ^Codes:
  # Match for codes
  ^\s+\S+\s+-\s+\S+
  ^\s*$$ -> Routes
  ^. -> Error

Routes
  ^\s*!\s+IP\s+routing\s+not\s+enabled -> Next
  ^\s+(\S+\s\S+?|\w?)\s+(\d+\.\d+\.\d+\.\d+)/(\d+)\s -> Continue.Record
  ^\s+${PROTOCOL}\s+${NETWORK}/${MASK}\s+is\s+${DIRECT}\s+${NEXT_HOP},\s+${INTERFACE}$$
  ^\s+${PROTOCOL}\s+${NETWORK}/${MASK}\s+(?:\[${DISTANCE}/${METRIC}\]|is\s+${DIRECT})(?:.+?)${NEXT_HOP}(?:\s+VNI\s+${VNI}\s+router-mac\s+${ROUTER_MAC}|,\s+${INTERFACE})$$
  ^\s+via\s+${NEXT_HOP}(?:\s+VNI\s+${VNI}\s+router-mac\s+${ROUTER_MAC}|,\s+${INTERFACE})
  ^\s*$$ -> Record
  ^VRF(\s+name)?:\s+${VRF}\s*$$ -> Start
  ^Gateway\s+of\s+last
  ^. -> Error

